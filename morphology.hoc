// Bladder Small DRG Neuron Soma (~ C-Type Fibre Soma) Model// 
// Mandge and Manchanda, 2018
// by Darshan Mandge
// Computational Neurophsyiology Lab, 
// IIT Bombay, Mumbai, India 400076
// email: darshanmandge@iitb.ac.in

begintemplate drg

public soma

////////////////////////
//                    //
// tau look up tables //
//                    //
////////////////////////

//CaN
objref vecv2_CaN, vecmtau_CaN, vechtau_CaN
vecv2_CaN = new Vector()
vecv2_CaN.indgen(-30, 20, 10)

vechtau_CaN = new Vector()
vechtau_CaN.append(60.00, 50.00, 44.91, 21.14, 22.26, 26.9)
table_tabhtau_CaN(&vechtau_CaN.x[0], vecv2_CaN.size, &vecv2_CaN.x[0])

//CaR
objref vecv_CaR, vechtau_CaR, vechtau2_CaR, vecmtau_CaR
vecv_CaR = new Vector()
vecv_CaR.indgen(-20, 20, 10) // vsrcdest.indgen(start,stop,stepsize)

vechtau_CaR = new Vector()
vechtau_CaR.append(828.96717,662.18,430.74661,513.46502,473.16638)  // 5. Hilliare+Dichot_17012016
table_tabhtau_CaR(&vechtau_CaR.x[0], vecv_CaR.size, &vecv_CaR.x[0])

vechtau2_CaR = new Vector()
vechtau2_CaR.append(20.85107,17.19,12.63647,17.82774,16.75605)  // 6. Hilliare+Dichot_17012016
table_tabhtau2_CaR(&vechtau2_CaR.x[0], vecv_CaR.size, &vecv_CaR.x[0])

vecmtau_CaR = new Vector()
vecmtau_CaR.append(0.745,2.087864,1.044556,1.5,0.796224)  // 5. Hilliare+Dichot_17012016
table_tabmtau_CaR(&vecmtau_CaR.x[0], vecv_CaR.size, &vecv_CaR.x[0])

//skca3
objref veccai_skca3, vecsf_skca3, vecvh_skca3
veccai_skca3 = new Vector()
veccai_skca3.append(0.2e-3, 0.3e-3, 0.5e-3, 10e-3)

vecvh_skca3 = new Vector()
vecvh_skca3.append(24, 35.30399, 60.49381, 59.13068)
table_tabvh_skca3(&vecvh_skca3.x[0], veccai_skca3.size, &veccai_skca3.x[0])

vecsf_skca3 = new Vector()
vecsf_skca3.append(128, 38.80141,48.77538,44.82705)
table_tabsf_skca3(&vecsf_skca3.x[0], veccai_skca3.size, &veccai_skca3.x[0])


//CaT
objref vecv_CaT, vecmtau_CaT, vecvh_CaT, vechtau_CaT
vecv_CaT = new Vector()
vecv_CaT.indgen(-65, 10, 5)

vecmtau_CaT = new Vector()
vecmtau_CaT.append(6.73,6.73,4.37,2.9,2.27,1.87,1.47,1.27,1.2,1.1,1.2,1.2,1.1,1.1,1.1,1.1)
table_tabmtau_CaT(&vecmtau_CaT.x[0], vecv_CaT.size, &vecv_CaT.x[0])

vechtau_CaT = new Vector()
vecvh_CaT 	= new Vector()

vecvh_CaT.append(-70.18405 ,-60 ,-49.93865 ,-40 ,-29.93865 ,-19.8773 ,-9.81595 ,0.1227 ,10.18405 ,20.2454 ,30.18405 ,40.2454)
vechtau_CaT.append(167.54378 ,61.65778 ,44.39809 ,31.05863 ,25.83774 ,21.80703 ,17.62502 ,14.5562 ,15.25575 ,12.60002 ,12.92211 ,13.54313)

table_tabhtau_CaT(&vechtau_CaT.x[0], vecvh_CaT.size, &vecvh_CaT.x[0])


//////////////////////
//                  //
// bladder drg soma //
//                  //
//////////////////////

proc init() {
create soma
soma{
	L	 = 24
	diam = 24 // 24um Yoshimura 1999 fig 3A
	nseg = 1
	cm	 = 28/(3.14* diam^2 )* 100 // 28 pF Yunoki-2014. For Converting pF/ um2 to uF/cm2. Multiplication factor is 100. The calculated value is 1.548 uF/cm2
	// cm_napkpump uses cm. Be careful while changing this value
	celsius = 22 // Important don't remove this.
	Ra 	    = 100 // Du 2014 	//Check the value of Ra for DRG neuron


	insert pas
	{
		g_pas = 0.0001
		e_pas = -41.583 // Calculated from the custom init() block. See 
	}


	//Na Channels
	insert nattxs
	{
		gbar_nattxs	= 0.0001
	}

	insert nav1p8
	{
		gbar_nav1p8	= 0.0087177
	}

	insert nav1p9
	{
		gbar_nav1p9	= 1e-5
	}

	// K+ Currents
	insert kaslow
	{
		gbar_kaslow	= 0.00136
	}
	insert kdr 
	{
		gbar_kdr		= 0.002688
	}

	insert hcn // Kouronova 2008
	{
		gbarfast_hcn	= 1.352e-5 
		gbarslow_hcn	= 6.7615e-06
	}

	//Calcium Channels
	insert CaL
	{
		pmax_CaL 		= 2.75e-5
	}
	insert CaN
	{
		pmax_CaN   	= 2.8e-5
	}
	insert CaPQ
	{
		pmax_CaPQ  	= 8e-6
	}
	insert CaR
	{
		pmax_CaR  		= 1e-08
	}
	insert CaT
	{
		pmax_CaT  		= 1e-08
	}
	insert bkca
	{
		gbar_bkca  	= 0.0009
	}
	insert kmtype
	{
		gbar_kmtype  	= 0.0001
	}
	insert knatype
	{
		gbar_knatype  	= 1e-05
	}
	insert nakpump // NaK Pump
	{
		capm_nakpump = cm(0.5) // Check this line
		gbar_nakpump	= 0.001
	}
	insert skca3
	{
		gbar_skca3		= 0.0009
	}
	insert cadyn   // new calcium dynamics mechanism
	{
		// comment out all blocks that are the same in the mod block
		//Ca Concentration initialization
		cai0_cadyn = cai
		cao0_cadyn = cao
		// PMCA
		//PMCA Parameters
		k1_cadyn = 3.74e7
		k2_cadyn = 2.5e5
		k3_cadyn = 500
		k4_cadyn = 5
		//IP3R SR values from Fink et al., 
		jmaxsr_cadyn = 3.5e-6 //ca Dynamics pdf Thayer
		kip3_cadyn = 0.0008 
		kactip3_cadyn = 0.0003
		konip3_cadyn = 2.7
		kinhip3_cadyn = 0.0002
		//Ca Diffusion Coefficient
		DCa_cadyn=0.6 
		//Cytoplasmic Ca buffering
		bbr_cadyn = 370 // Zeilhofer 2000 endogenous buffer binding ratio = 370 (Rat DRG); 
		//Mito Buffer
		Kmmt_cadyn = 0.01e-3
		Bmmt_cadyn = 0.065
		//ER Buffering
		Kmer_cadyn = 0.5
		Bmer_cadyn =  10
		//ER Ca Concentration
		caeri = 0.4
		caeri0_cadyn =0.4  //
		//SERCA
		kpsr_cadyn = 0.00027
		// cadyn.mod parameters
		pump0_cadyn 	= 4.232e-13		//PMCA
		//Calcium Dynamics Parameters
		//ER Parameters
		vmaxsr_cadyn 	= 3.75e-06   //SERCA
		vcicr_cadyn 	= 5e-07    //CICR
		//Mitochondria
		vmcu_cadyn		= 1.446825e-6 //MCU
		vncx_cadyn 	= 6e-5 		  //MNCX */
		kmcu_cadyn = 606e-6
		//Mitochondria Ca Concentration
		camti = 200e-6 
		camti0_cadyn = 200e-6	// 200 nM 
		//CICR
		// :Koch - Ch6- E de. Schutter pg34 (Purkinje Cell)
		kcicr_cadyn = 0.00198 // Lokutta 1998/9
		ktcicr_cadyn = 0.0006
		//MNCX
		kna_cadyn = 8
		kncx_cadyn = 0.035
	}
	// ANO1 CACC activating Ca Concentration
	// caip3ri is the ca change by ca rel. by outmost shell's IP3Rs read by ANO1 CACC
	caip3ri = cai
	insert ip3dif
	{
		ip3i0_ip3dif=0.16e-3
		DIP3_ip3dif =0.283	
		kdegr_ip3dif= 0.14e-3
	}
	insert ncxsoma
	{
		ImaxNax_ncxsoma= 1.1e-05
	}
	// Calcium activated chloride channel
	insert cacc 
	{
		gbar_cacc		= 1e-6
	}
	insert soce // Store operated calcium channel added on 29-10-2016
	{
		pmax_soce		= 1e-9
	}
	insert trpm8
	{
		gbar_trpm8		= 1e-7
	}
		//ions
	//For thayer 1990 (A-delta neurons) = 2
	cai = 136e-6 // ?? Remove either these statements or cai_ca_ion
	cao = 2 //
	cai0_ca_ion = 136e-6
	cao0_ca_ion = 2 // Previously used 10.3   2.5 = default value. 

	cli0_cl_ion = 40
	clo0_cl_ion = 145 // Delpire and Staley, 2014
	cli = 40		  // Deba and  Besac, 2015   //8
	clo = 145		  // gives ecl = -32.7
	ecl = -32.7

	ki0_k_ion = 140 // See chapter 8 NEURON Book -Initializing concentrations in hoc page 10 - It is a GLOBAL variable sets the inital conc
	ko0_k_ion = 5

	nai0_na_ion = 10
	nao0_na_ion = 150

	ko=5
	ki=140
	nao=150
	nai=10

	eh = -30
	ek=  -84.7
	ena= 68.83 
}
}
endtemplate drg